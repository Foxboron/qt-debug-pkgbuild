# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt5-virtualkeyboard
_qtver=5.14.0-beta3
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3')
pkgdesc='Virtual keyboard framework'
depends=('qt5-declarative' 'qt5-svg' 'hunspell')
makedepends=()
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/development_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
         qtvirtualkeyboard-fix-build.patch::"https://code.qt.io/cgit/qt/qtvirtualkeyboard.git/patch/?id=b4d8dc18")
sha256sums=('04ebd98acff10480418ac00f9b056f6b09fd1a55a7a69f0332c9dd8d330320ae'
            '08c11b9957573f786e23c239b999c96925c263e61b0d799003e260384a6633d3')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -Rp1 -i ../qtvirtualkeyboard-fix-build.patch # Revert commit that breaks build
}

build() {
  cd build

  qmake ../${_pkgfqn} CONFIG+="lang-all handwriting"
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;
}
