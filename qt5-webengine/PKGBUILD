# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_qtver=5.13.1
pkgver=${_qtver/-/}
pkgrel=3
arch=('x86_64')
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx')
makedepends=('python2' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        qtbug-77209.patch::"https://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=1c33a4a6"
        qtwebengine-pulseaudio-13.patch::"https://code.qt.io/cgit/qt/qtwebengine-chromium.git/patch/?id=0b57f159"
        qt5-webengine-linux-5.2.patch::"https://code.qt.io/cgit/qt/qtwebengine-chromium.git/patch?id=74e69da9")
sha256sums=('7f12e710b76ff2be4497bf368fc01ecd2419e2e962fb90599b0ece00ae2eb7d0'
            '9773e1fed9b8998dd800ea89315b4ea70ed81b568b0a740945ac76577c62e6f1'
            '7ed06cc2d0e3cc86df33bb80468a5f0422ed2cc5985db86444949ef3b9a12f02'
            '8deb3df43a379976bbdb1fa814e68b2ce0433043695efc694c11e47f67c51b6a')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -p1 -i ../qtbug-77209.patch # Fix QWebEnginePage visibility state
  cd src/3rdparty
  patch -p1 -i "$srcdir"/qtwebengine-pulseaudio-13.patch # Fix build with pulseaudio 13
  patch -p1 -i "$srcdir"/qt5-webengine-linux-5.2.patch # Fix build with recent kernel
}

build() {
  cd build

  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
